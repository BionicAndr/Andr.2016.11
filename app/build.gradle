buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
    }
}

// Required because retrolambda is on maven central
repositories {
    mavenCentral()
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.bionic.andr"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_TOKEN", "\"4e5b36491c29f44d227659690cb0bc07\""
        buildConfigField "String", "ICON_TEMPLATE", "\"http://openweathermap.org/img/w/%s.png\""

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
        }
    }

    flavorDimensions "api", "mode"

    productFlavors {
        prod {
            dimension "api"
            buildConfigField "String", "API_TOKEN", "\"4e5b36491c29f44d227659690cb0bc07\""
        }

        local {
            dimension "api"
            buildConfigField "String", "API_TOKEN", "\"4e5b36491c29f44d227659690cb0bc07\""
        }

        logging {
            dimension "mode"
            buildConfigField "boolean", "LOGGING", "true"
        }

        silent {
            dimension "mode"
            buildConfigField "boolean", "LOGGING", "false"
        }

    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

}

task checkstyle(type: Checkstyle) {
    configFile project.file('../config/checkstyle.xml')
    showViolations true
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'
    exclude '**/org/**'

    def configProps = ['proj.module.dir': projectDir.absolutePath]
    configProperties configProps

    // empty classpath
    classpath = files()
}
preBuild.dependsOn('checkstyle')

dependencies {

    compile project(':rxperm')

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile 'com.android.support:support-v4:25.1.0'
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:recyclerview-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:support-vector-drawable:25.1.0'

    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    compile 'com.jakewharton:butterknife:8.4.0'

    compile 'com.google.dagger:dagger:2.8'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.8'

    compile 'io.reactivex:rxandroid:1.2.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'com.jakewharton.rxbinding:rxbinding:1.0.0'
    compile 'com.f2prateek.rx.receivers:rx-receivers:0.1.0'
    compile 'com.trello:rxlifecycle-android:1.0'
    compile 'com.trello:rxlifecycle-components:1.0'
    // StorIO
    compile 'com.pushtorefresh.storio:sqlite:1.12.1'
    compile 'com.pushtorefresh.storio:sqlite-annotations:1.12.1'
    annotationProcessor 'com.pushtorefresh.storio:sqlite-annotations-processor:1.12.1'

    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
}
